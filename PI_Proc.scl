FUNCTION_BLOCK "PI_Proc"
TITLE='350-2脉冲测量'
VERSION:'0.11'
KNOW_HOW_PROTECT
AUTHOR:Goosy
NAME:CNTProc
FAMILY:GoosyLib

VAR_INPUT
  DB_NO : WORD;   // 350-2模块专用数据块块号
  equS1 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix1 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS2 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix2 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS3 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix3 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS4 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix4 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS5 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix5 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS6 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix6 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS7 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix7 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
  equS8 {S7_m_c := 'true'} : REAL := 10000.0;   // 脉冲当量 个/立方米
  fFix8 {S7_m_c := 'true'} : REAL := 1.0;       // 修正系数
END_VAR

VAR_OUTPUT
  flow_rate1 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate2 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate3 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate4 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate5 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate6 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate7 {S7_m_c := 'true'} : REAL;       // 瞬时流量
  flow_rate8 {S7_m_c := 'true'} : REAL;       // 瞬时流量
END_VAR

VAR_IN_OUT
  flow1 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow2 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow3 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow4 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow5 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow6 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow7 {S7_m_c := 'true'} : REAL;       // 累积流量
  flow8 {S7_m_c := 'true'} : REAL;       // 累积流量
END_VAR

VAR
  flowRef1 : REAL := 0.0;       // 参照流量
  flowRef2 : REAL := 0.0;       // 参照流量
  flowRef3 : REAL := 0.0;       // 参照流量
  flowRef4 : REAL := 0.0;       // 参照流量
  flowRef5 : REAL := 0.0;       // 参照流量
  flowRef6 : REAL := 0.0;       // 参照流量
  flowRef7 : REAL := 0.0;       // 参照流量
  flowRef8 : REAL := 0.0;       // 参照流量
  cntRef1 : DINT := 0;       // 参照计数
  cntRef2 : DINT := 0;       // 参照计数
  cntRef3 : DINT := 0;       // 参照计数
  cntRef4 : DINT := 0;       // 参照计数
  cntRef5 : DINT := 0;       // 参照计数
  cntRef6 : DINT := 0;       // 参照计数
  cntRef7 : DINT := 0;       // 参照计数
  cntRef8 : DINT := 0;       // 参照计数
  JOB_RD_BUSY : BYTE := B#16#64;   // 切换通道标志
  rd_ret : INT;
END_VAR

VAR_TEMP
  rawCount : DINT;       // 模块计数
  newFlow : REAL;      // 计算新累计流量
END_VAR

CONST
  factor := 3.6; // 系数3.6 = 3600(秒数/小时) / 1000(窗口期)
END_CONST

BEGIN
  //设置模块号
  "CNT2_CTR"(DB_NO := DB_NO);
  //打开软件门开始计数
  WORD_TO_BLOCK_DB(DB_NO).DBB23 := B#16#FF;

  // 模块通道转换,并读取
  IF NOT WORD_TO_BLOCK_DB(DB_NO).DBX3.0 THEN
    JOB_RD_BUSY:= JOB_RD_BUSY XOR B#16#1; //B#16#64 or B#16#65
  END_IF;
  WORD_TO_BLOCK_DB(DB_NO).DBB2 := JOB_RD_BUSY;
  rd_ret := "CNT2_RD"(DB_NO := DB_NO); 

  // 瞬时流量
  flow_rate1 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD152) * fFix1 / equS1 * factor;
  flow_rate2 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD160) * fFix2 / equS2 * factor;
  flow_rate3 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD168) * fFix3 / equS3 * factor;
  flow_rate4 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD176) * fFix4 / equS4 * factor;
  flow_rate5 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD184) * fFix5 / equS5 * factor;
  flow_rate6 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD192) * fFix6 / equS6 * factor;
  flow_rate7 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD200) * fFix7 / equS7 * factor;
  flow_rate8 := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD208) * fFix8 / equS8 * factor;

  // 累积流量
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD148);
  newFlow := flowRef1 + (rawCount - cntRef1) * fFix1 / equS1;
  IF (newFlow - flow1) > 1.0 OR (newFlow - flow1) < -1.0 THEN
    flowRef1 := flow1;
    cntRef1 := rawCount;
  ELSE
    flow1 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD156);
  newFlow := flowRef2 + (rawCount - cntRef2) * fFix2 / equS2;
  IF (newFlow - flow2) > 1.0 OR (newFlow - flow2) < -1.0 THEN
    flowRef2 := flow2;
    cntRef2 := rawCount;
  ELSE
    flow2 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD164);
  newFlow := flowRef3 + (rawCount - cntRef3) * fFix3 / equS3;
  IF (newFlow - flow3) > 1.0 OR (newFlow - flow3) < -1.0 THEN
    flowRef3 := flow3;
    cntRef3 := rawCount;
  ELSE
    flow3 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD172);
  newFlow := flowRef4 + (rawCount - cntRef4) * fFix4 / equS4;
  IF (newFlow - flow4) > 1.0 OR (newFlow - flow4) < -1.0 THEN
    flowRef4 := flow4;
    cntRef4 := rawCount;
  ELSE
    flow4 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD180);
  newFlow := flowRef5 + (rawCount - cntRef5) * fFix5 / equS5;
  IF (newFlow - flow5) > 1.0 OR (newFlow - flow5) < -1.0 THEN
    flowRef5 := flow5;
    cntRef5 := rawCount;
  ELSE
    flow5 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD188);
  newFlow := flowRef6 + (rawCount - cntRef6) * fFix6 / equS6;
  IF (newFlow - flow6) > 1.0 OR (newFlow - flow6) < -1.0 THEN
    flowRef6 := flow6;
    cntRef6 := rawCount;
  ELSE
    flow6 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD196);
  newFlow := flowRef7 + (rawCount - cntRef7) * fFix7 / equS7;
  IF (newFlow - flow7) > 1.0 OR (newFlow - flow7) < -1.0 THEN
    flowRef7 := flow7;
    cntRef7 := rawCount;
  ELSE
    flow7 := newFlow;
  END_IF;
  rawCount := DWORD_TO_DINT(WORD_TO_BLOCK_DB(DB_NO).DBD204);
  newFlow := flowRef8 + (rawCount - cntRef8) * fFix8 / equS8;
  IF (newFlow - flow8) > 1.0 OR (newFlow - flow8) < -1.0 THEN
    flowRef8 := flow8;
    cntRef8 := rawCount;
  ELSE
    flow8 := newFlow;
  END_IF;
END_FUNCTION_BLOCK
